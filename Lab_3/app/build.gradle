apply plugin: 'com.android.application'
apply plugin: 'com.google.gms.google-services'

def getVersionCode = { ->
    def stdout = new ByteArrayOutputStream()

    exec {
        commandLine 'git', 'tag', '--list'
        standardOutput = stdout
    }

    return stdout.toString().split("\n").size()
}

def getVersionName = { ->
    def stdout = new ByteArrayOutputStream()

    exec {
        commandLine 'git', 'describe', '--tags', '--long'
        standardOutput = stdout
    }

    stdout = stdout.toString().trim()
    stdout = stdout.replaceAll(stdout.charAt(stdout.indexOf('-')).toString(), '.')

    return stdout[0..(stdout.lastIndexOf('.') - 1)]
}

android {
    compileSdkVersion 30
    buildToolsVersion "30.0.2"

    defaultConfig {
        applicationId "by.bsuir.wildboom.lab3"
        minSdkVersion 19
        targetSdkVersion 30
        multiDexEnabled true
        versionCode getVersionCode()
        versionName getVersionName()

        testInstrumentationRunner "androidx.test.runner.AndroidJUnitRunner"
        signingConfig signingConfigs.debug
    }

    buildTypes {
        release {
            minifyEnabled false
            proguardFiles getDefaultProguardFile('proguard-android-optimize.txt'), 'proguard-rules.pro'
        }
    }
}

dependencies {
    implementation fileTree(dir: "libs", include: ["*.jar"])
    implementation 'androidx.appcompat:appcompat:1.2.0'
    implementation 'androidx.constraintlayout:constraintlayout:2.0.4'
    implementation 'androidx.lifecycle:lifecycle-extensions:2.2.0'
    testImplementation 'junit:junit:4.12'
    androidTestImplementation 'androidx.test.ext:junit:1.1.2'
    androidTestImplementation 'androidx.test.espresso:espresso-core:3.3.0'
    implementation platform('com.google.firebase:firebase-bom:26.0.0')
    implementation 'com.google.firebase:firebase-analytics'
    implementation 'com.google.firebase:firebase-auth'
    implementation 'com.google.firebase:firebase-firestore'
    implementation 'com.google.firebase:firebase-storage:19.2.1'
    implementation 'androidx.multidex:multidex:2.0.1'
    implementation 'com.timgroup:jgravatar:1.0'
    implementation 'com.squareup.picasso:picasso:2.5.2'
}

